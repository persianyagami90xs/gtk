expand_content_md_files = [
]

gdk4_toml = configure_file(input: 'gdk4.toml.in', output: 'gdk4.toml', configuration: toml_conf)

if get_option('gtk_doc')
  custom_target('gdk4-doc',
    input: [ gdk4_toml, gdk_gir[0] ],
    output: 'gdk4',
    command: [
      gidocgen,
      'generate',
      '--quiet',
      '--add-include-path=@0@'.format(meson.current_build_dir() / '../../../gtk'),
      '--config=@INPUT0@',
      '--output-dir=@OUTPUT@',
      '--no-namespace-dir',
      '--content-dir=@0@'.format(meson.current_source_dir()),
      '@INPUT1@',
    ],
    depend_files: [ expand_content_md_files ],
    build_by_default: true,
  )

  if x11_enabled
    custom_target('gdk4-x11-doc',
      input: [ gdk4_toml, gdk_x11_gir[0] ],
      output: 'gdk4-x11',
      command: [
        gidocgen,
        'generate',
        '--quiet',
        '--add-include-path=@0@'.format(meson.current_build_dir() / '../../../gtk'),
        '--config=@INPUT0@',
        '--output-dir=@OUTPUT@',
        '--no-namespace-dir',
        '--content-dir=@0@'.format(meson.current_source_dir()),
        '@INPUT1@',
      ],
      depends: [ gdk_gir[0] ],
      depend_files: [ expand_content_md_files ],
      build_by_default: true,
    )
  endif

  if wayland_enabled
    custom_target('gdk4-wayland-doc',
      input: [ gdk4_toml, gdk_wayland_gir[0] ],
      output: 'gdk4-wayland',
      command: [
        gidocgen,
        'generate',
        '--quiet',
        '--add-include-path=@0@'.format(meson.current_build_dir() / '../../../gtk'),
        '--config=@INPUT0@',
        '--output-dir=@OUTPUT@',
        '--no-namespace-dir',
        '--content-dir=@0@'.format(meson.current_source_dir()),
        '@INPUT1@',
      ],
      depends: [ gdk_gir[0] ],
      depend_files: [ expand_content_md_files ],
      build_by_default: true,
    )
  endif
endif
